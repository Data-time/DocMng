{
 'Реквизиты': {
  'Наименование': 'Контроль - заполнение пользователей',
  'ТекстОбработки': '//------------------------------------------------------------------------------
	//ПАРАМЕТРЫ                                          
	//------------------------------------------------------------------------------
	Отладка = Ложь;   
	 
	ДатаНачалаВыгрузки = ТекущаяДата();
	ТемаСообщения = ""+Система+" - Неправильно заведенные пользователи 1С "+Формат(ДатаНачалаВыгрузки,"ДФ=dd.MM.yyyy");  
	
	ПочтаПолучателей = "ssmirnov@t-medica.com";   
	 
	ГуидУчетнойЗаписиПочты = "567e3146-123f-11ed-a12c-ef295ee2b5a3"; //предопределенная
	 
    Если Отладка Тогда 
    	ПочтаПолучателей = "ssmirnov@t-medica.com";
    КонецЕсли;
	
	СимволПереносаСтроки = Символы.ПС + Символы.ВК;
	СимволПереносаСтроки = Символы.ПС;  
	
	ТекстСообщения = "Выполняются проверки:"+СимволПереносаСтроки+
	"	1. Пустой пароль 1С - у всех активных пользователей"+СимволПереносаСтроки+
	"	2. Пустой email - у всех активных не служебных пользователей"+СимволПереносаСтроки+
	"	3. Не равно имя и полное имя пользователя - у всех активных не служебных пользователей"+СимволПереносаСтроки+
	"	4. ФИО должно состоять из 3 слов - у всех активных не служебных пользователей"+СимволПереносаСтроки+
	"	5. Не должно быть прав администратора - у всех активных, не служебных и не администраторов 1С "+СимволПереносаСтроки+ 
	"	6. Служебный пользователь не должен быть показан в списке выбора - у всех активныхпользователей"+СимволПереносаСтроки+	
	"ПользовательАдминистратор1С и СлужебныйПользователь - дополнительные реквизиты"+СимволПереносаСтроки+
	СимволПереносаСтроки;
	
	//------------------------------------------------------------------------------
	//Сбор данных
	//------------------------------------------------------------------------------  
	  
	//сложно создавать ТЗ через КОМ, поэтому создаю тут и преобразовываю в КОМ 
	    
	ТЗПользователейИБ = Новый ТаблицаЗначений;
	ТЗПользователейИБ.Колонки.Добавить("УникальныйИдентификатор",новый ОписаниеТипов("УникальныйИдентификатор"));
	ТЗПользователейИБ.Колонки.Добавить("Имя",новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
	ТЗПользователейИБ.Колонки.Добавить("ПолноеИмя",новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
	ТЗПользователейИБ.Колонки.Добавить("АдресЭлектроннойПочты",новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
	ТЗПользователейИБ.Колонки.Добавить("АутентификацияСтандартная",новый ОписаниеТипов("Булево"));
	ТЗПользователейИБ.Колонки.Добавить("ПарольУстановлен",новый ОписаниеТипов("Булево"));	
	ТЗПользователейИБ.Колонки.Добавить("АутентификацияOpenID",новый ОписаниеТипов("Булево"));
	ТЗПользователейИБ.Колонки.Добавить("АутентификацияОС",новый ОписаниеТипов("Булево"));
	ТЗПользователейИБ.Колонки.Добавить("ПоказыватьВСпискеВыбора",новый ОписаниеТипов("Булево"));
	
	ТЗПользователейИБ.Колонки.Добавить("КолвоСловВИмени",новый ОписаниеТипов("Число"));            
	ТЗПользователейИБ.Колонки.Добавить("ЕстьПраваАдминистратора",новый ОписаниеТипов("Булево"));
	
	
	//перенести comТЗ в ТЗ
	СтрВнутр = ЗначениеВСтрокуВнутр(ТЗПользователейИБ);
	ТЗПользователейИБ_КОМ = Соединение.ЗначениеИзСтрокиВнутр(СтрВнутр);
	
	МассивАдминскихРолей_КОМ = Соединение.NewObject("Массив");
	МассивАдминскихРолей_КОМ.Добавить(Соединение.Метаданные.Роли.Найти("ПолныеПрава"));
	МассивАдминскихРолей_КОМ.Добавить(Соединение.Метаданные.Роли.Найти("АдминистраторСистемы"));
	МассивАдминскихРолей_КОМ.Добавить(Соединение.Метаданные.Роли.Найти("Администрирование"));
	
	ВсеПользователиИБ_КОМ = Соединение.ПользователиИнформационнойБазы.ПолучитьПользователей(); 
	Для каждого ПользовательИБ_КОМ из ВсеПользователиИБ_КОМ Цикл 
		НовСтрТЗ_КОМ = ТЗПользователейИБ_КОМ.Добавить();
		НовСтрТЗ_КОМ.УникальныйИдентификатор = ПользовательИБ_КОМ.УникальныйИдентификатор;
		НовСтрТЗ_КОМ.Имя = ПользовательИБ_КОМ.Имя;
		НовСтрТЗ_КОМ.ПолноеИмя = ПользовательИБ_КОМ.ПолноеИмя;
		НовСтрТЗ_КОМ.АдресЭлектроннойПочты = ПользовательИБ_КОМ.АдресЭлектроннойПочты;
		НовСтрТЗ_КОМ.АутентификацияСтандартная = ПользовательИБ_КОМ.АутентификацияСтандартная;
		НовСтрТЗ_КОМ.ПарольУстановлен = ПользовательИБ_КОМ.ПарольУстановлен;  
		НовСтрТЗ_КОМ.АутентификацияOpenID = ПользовательИБ_КОМ.АутентификацияOpenID;
		НовСтрТЗ_КОМ.АутентификацияОС = ПользовательИБ_КОМ.АутентификацияОС; 
		НовСтрТЗ_КОМ.ПоказыватьВСпискеВыбора = ПользовательИБ_КОМ.ПоказыватьВСпискеВыбора; 
		
		НовСтрТЗ_КОМ.КолвоСловВИмени = СтрРазделить(НовСтрТЗ_КОМ.ПолноеИмя," ",Истина).Количество();	
		//проверка админских прав
		ЕстьПраваАдминистратора = Ложь;
		Для каждого АдминскаяРоль Из МассивАдминскихРолей_КОМ Цикл
			Если ПользовательИБ_КОМ.Роли.Содержит(АдминскаяРоль) Тогда 
				ЕстьПраваАдминистратора = истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		НовСтрТЗ_КОМ.ЕстьПраваАдминистратора = ЕстьПраваАдминистратора;
	КонецЦикла;
	
	
	Запрос_КОМ = Соединение.NewObject("Запрос");
	Запрос_КОМ.Текст = 
		"ВЫБРАТЬ
		|	ТЗПользователейИБ.Имя КАК Имя,
		|	ТЗПользователейИБ.ПолноеИмя КАК ПолноеИмя,
		|	ТЗПользователейИБ.УникальныйИдентификатор КАК УникальныйИдентификатор,
		|	ТЗПользователейИБ.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты,
		|	ТЗПользователейИБ.АутентификацияСтандартная КАК АутентификацияСтандартная,
		|	ТЗПользователейИБ.ПарольУстановлен КАК ПарольУстановлен,
		|	ТЗПользователейИБ.АутентификацияOpenID КАК АутентификацияOpenID,
		|	ТЗПользователейИБ.АутентификацияОС КАК АутентификацияОС,
		|	ТЗПользователейИБ.ПоказыватьВСпискеВыбора КАК ПоказыватьВСпискеВыбора,
		|	ТЗПользователейИБ.КолвоСловВИмени КАК КолвоСловВИмени,
		|	ТЗПользователейИБ.ЕстьПраваАдминистратора КАК ЕстьПраваАдминистратора
		|ПОМЕСТИТЬ ВТПользователиИБ
		|ИЗ
		|	&ТЗПользователейИБ КАК ТЗПользователейИБ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПользователиИБ.Имя КАК Имя,
		|	ВТПользователиИБ.ПолноеИмя КАК ПолноеИмя,
		|	ВТПользователиИБ.УникальныйИдентификатор КАК УникальныйИдентификатор,
		|	ВТПользователиИБ.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты,
		|	ВТПользователиИБ.АутентификацияСтандартная КАК АутентификацияСтандартная,
		|	ВТПользователиИБ.ПарольУстановлен КАК ПарольУстановлен,
		|	ВТПользователиИБ.АутентификацияOpenID КАК АутентификацияOpenID,
		|	ВТПользователиИБ.АутентификацияОС КАК АутентификацияОС,
		|	Пользователи.Ссылка КАК ПользовательСсылка,
		|	МАКСИМУМ(ЕСТЬNULL(ПользователиКонтактнаяИнформация.АдресЭП, """")) КАК Email,
		|	Пользователи.Служебный КАК Служебный,
		|	ВТПользователиИБ.КолвоСловВИмени КАК КолвоСловВИмени,
		|	ВТПользователиИБ.ЕстьПраваАдминистратора КАК ЕстьПраваАдминистратора,
		|	МАКСИМУМ(ЕСТЬNULL(ВЫРАЗИТЬ(ПользователиДР_Администратор1С.Значение КАК БУЛЕВО), ЛОЖЬ)) КАК ЭтоАдминистратор1С,
		|	МАКСИМУМ(ЕСТЬNULL(ВЫРАЗИТЬ(ПользователиДР_Служебный.Значение КАК БУЛЕВО), ЛОЖЬ)) ИЛИ Пользователи.Служебный КАК ЭтоСлужебный,
		|	ВТПользователиИБ.ПоказыватьВСпискеВыбора КАК ПоказыватьВСпискеВыбора
		|ПОМЕСТИТЬ ВТАктивныеПользователи
		|ИЗ
		|	ВТПользователиИБ КАК ВТПользователиИБ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
		|			ПО Пользователи.Ссылка = ПользователиКонтактнаяИнформация.Ссылка
		|				И (ПользователиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
		|				И (ПользователиКонтактнаяИнформация.АдресЭП <> """")
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи.ДополнительныеРеквизиты КАК ПользователиДР_Администратор1С
		|			ПО Пользователи.Ссылка = ПользователиДР_Администратор1С.Ссылка
		|				И (ПользователиДР_Администратор1С.Свойство = &СвойствоПользовательАдминистратор1С)
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи.ДополнительныеРеквизиты КАК ПользователиДР_Служебный
		|			ПО Пользователи.Ссылка = ПользователиДР_Служебный.Ссылка
		|				И (ПользователиДР_Служебный.Свойство = &СвойствоСлужебныйПользователь)
		|		ПО ВТПользователиИБ.УникальныйИдентификатор = Пользователи.ИдентификаторПользователяИБ
		|ГДЕ
		|	НЕ Пользователи.Недействителен
		|	И (ВТПользователиИБ.АутентификацияСтандартная
		|			ИЛИ ВТПользователиИБ.АутентификацияOpenID
		|			ИЛИ ВТПользователиИБ.АутентификацияОС)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПользователиИБ.Имя,
		|	ВТПользователиИБ.ПолноеИмя,
		|	ВТПользователиИБ.УникальныйИдентификатор,
		|	ВТПользователиИБ.АдресЭлектроннойПочты,
		|	ВТПользователиИБ.АутентификацияСтандартная,
		|	ВТПользователиИБ.ПарольУстановлен,
		|	ВТПользователиИБ.АутентификацияOpenID,
		|	ВТПользователиИБ.АутентификацияОС,
		|	Пользователи.Ссылка,
		|	Пользователи.Служебный,
		|	ВТПользователиИБ.КолвоСловВИмени,
		|	ВТПользователиИБ.ЕстьПраваАдминистратора,
		|	ВТПользователиИБ.ПоказыватьВСпискеВыбора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТАктивныеПользователи.Имя КАК Имя,
		|	ВТАктивныеПользователи.ПолноеИмя КАК ПолноеИмя,
		|	ВТАктивныеПользователи.ПользовательСсылка КАК ПользовательСсылка,
		|	""Пустой пароль 1С"" КАК Нарушение
		|ИЗ
		|	ВТАктивныеПользователи КАК ВТАктивныеПользователи
		|ГДЕ
		|	ВТАктивныеПользователи.АутентификацияСтандартная
		|	И НЕ ВТАктивныеПользователи.ПарольУстановлен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТАктивныеПользователи.Имя,
		|	ВТАктивныеПользователи.ПолноеИмя,
		|	ВТАктивныеПользователи.ПользовательСсылка,
		|	""Не установлен email""
		|ИЗ
		|	ВТАктивныеПользователи КАК ВТАктивныеПользователи
		|ГДЕ
		|	ВТАктивныеПользователи.Email = """"
		|	И НЕ ВТАктивныеПользователи.ЭтоСлужебный
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТАктивныеПользователи.Имя,
		|	ВТАктивныеПользователи.ПолноеИмя,
		|	ВТАктивныеПользователи.ПользовательСсылка,
		|	""Имя пользователя отличается от имени пользователя ИБ""
		|ИЗ
		|	ВТАктивныеПользователи КАК ВТАктивныеПользователи
		|ГДЕ
		|	НЕ ВТАктивныеПользователи.ЭтоСлужебный
		|	И ВТАктивныеПользователи.ПолноеИмя <> ВТАктивныеПользователи.Имя
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТАктивныеПользователи.Имя,
		|	ВТАктивныеПользователи.ПолноеИмя,
		|	ВТАктивныеПользователи.ПользовательСсылка,
		|	""Имя должно состоять из 3 слов: ФИО""
		|ИЗ
		|	ВТАктивныеПользователи КАК ВТАктивныеПользователи
		|ГДЕ
		|	НЕ ВТАктивныеПользователи.ЭтоСлужебный
		|	И ВТАктивныеПользователи.КолвоСловВИмени <> 3
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТАктивныеПользователи.Имя,
		|	ВТАктивныеПользователи.ПолноеИмя,
		|	ВТАктивныеПользователи.ПользовательСсылка,
		|	""Пользователь (не администратор 1С) имеет админские права""
		|ИЗ
		|	ВТАктивныеПользователи КАК ВТАктивныеПользователи
		|ГДЕ
		|	НЕ ВТАктивныеПользователи.ЭтоСлужебный
		|	И ВТАктивныеПользователи.ЕстьПраваАдминистратора
		|	И НЕ ВТАктивныеПользователи.ЭтоАдминистратор1С
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТАктивныеПользователи.Имя,
		|	ВТАктивныеПользователи.ПолноеИмя,
		|	ВТАктивныеПользователи.ПользовательСсылка,
		|	""Слежубный пользователь не должен быть показан в списке выбора""
		|ИЗ
		|	ВТАктивныеПользователи КАК ВТАктивныеПользователи
		|ГДЕ
		|	ВТАктивныеПользователи.ЭтоСлужебный
		|	И ВТАктивныеПользователи.ПоказыватьВСпискеВыбора
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПолноеИмя"; 
	
	Запрос_КОМ.УстановитьПараметр("ТЗПользователейИБ",ТЗПользователейИБ_КОМ);
	Запрос_КОМ.УстановитьПараметр("СвойствоПользовательАдминистратор1С",Соединение.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ПользовательАдминистратор1С"));	
    Запрос_КОМ.УстановитьПараметр("СвойствоСлужебныйПользователь",Соединение.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","СлужебныйПользователь"));	

	Выборка_КОМ = Запрос_КОМ.Выполнить().Выбрать();
	ПП=0;
	Пока Выборка_КОМ.Следующий() Цикл
		ПП=ПП+1;
		НавСсылка = Соединение.ПолучитьНавигационнуюСсылку(Выборка_КОМ.ПользовательСсылка);
		ТекстСообщения = ТекстСообщения
			+ ПП +"."+ Символы.ТАБ  
			+ Выборка_КОМ.ПолноеИмя + Символы.ТАБ
			+ Выборка_КОМ.Нарушение + Символы.ТАБ
			+ НавСсылка + СимволПереносаСтроки;
	КонецЦикла; 
	   
	//Отправка письма
	Если Выборка_КОМ.Количество() > 0 Тогда		 					  
		Алгоритм("ОтправитьEmail",ЗапросСсылка,ПочтаПолучателей,ТемаСообщения,ТекстСообщения,ТипТекстаПочтовогоСообщения.ПростойТекст,ГуидУчетнойЗаписиПочты); 
		СтрокаОшибок = СтрокаОшибок + ""+Система+" - сообщения отправлены"+Символы.ПС;
	Иначе
		СтрокаОшибок = СтрокаОшибок + ""+Система+" - нет данных для отправки"+Символы.ПС;
	КонецЕсли;	
	
	Если не Отладка Тогда
		УстановитьДатуПоследнейВыгрузки(Система,ЗапросСсылка,ДатаНачалаВыгрузки);
	КонецЕсли;',
  'Комментарий': ''
 },
 'ТЧ': [
  {
   'Переменные': []
  }
 ]
}
