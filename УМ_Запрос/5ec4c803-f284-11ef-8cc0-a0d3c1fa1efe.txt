{
 'Реквизиты': {
  'Наименование': 'Интеграция с УПП (Возврат оборудования - Оповещение по "Ожидание оформления возврата прибора")',
  'ТекстОбработки': 'ДанныеПараметров = Новый Структура;
Для каждого ДанныеПеременной Из Переменные Цикл
	ДанныеПараметров.Вставить(ДанныеПеременной.Имя, ДанныеПеременной.Значение);
КонецЦикла;
	
Запрос = Новый Запрос;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТекущиеСостоянияДокументов.Документ КАК Документ,
	|	ТекущиеСостоянияДокументов.ДатаУстановки КАК ДатаУстановки,
	|	ТекущиеСостоянияДокументов.Документ.Подготовил КАК Подготовил,
	|	DT_Данные_ВозвратОборудования.ОсновнойМенеджерКонтрагента КАК ОсновнойМенеджерКонтрагента,
	|	DT_Данные_ВозвратОборудования.ОтветственныйЗаКатегорию КАК ОтветственныйЗаКатегорию
	|ИЗ
	|	РегистрСведений.ТекущиеСостоянияДокументов КАК ТекущиеСостоянияДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.DT_ДопНастройкиВидовДокументов КАК DT_ДопНастройкиВидовДокументов
	|		ПО ТекущиеСостоянияДокументов.Документ.ВидДокумента = DT_ДопНастройкиВидовДокументов.Владелец
	|			И (DT_ДопНастройкиВидовДокументов.ФункционалВозвратаОборудования)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДействияИсполнения КАК ДействияИсполнения
	|		ПО ТекущиеСостоянияДокументов.Действие = ДействияИсполнения.Ссылка
	|			И (ДействияИсполнения.ВидДействия = &ВидДействия)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.DT_Данные_ВозвратОборудования КАК DT_Данные_ВозвратОборудования
	|		ПО ТекущиеСостоянияДокументов.Документ = DT_Данные_ВозвратОборудования.Владелец
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбработкиОбъектов КАК ОбработкиОбъектов
	|		ПО ТекущиеСостоянияДокументов.Документ = ОбработкиОбъектов.Владелец
	|			И (НЕ ОбработкиОбъектов.ПомещенаВИсторию)
	|ГДЕ
	|	ТекущиеСостоянияДокументов.Состояние = &Состояние";
Запрос.УстановитьПараметр("ВидДействия", УМ_ДинамическиеКонстантыПовтИсп.ПолучитьЗначениеДинамическойКонстанты("ВозвратОборудования_ОформлениеВозврата"));
Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияДокументов.НаИсполнении);

ПериодичностьОповещенияВДнях = 0;
Если ДанныеПараметров.Свойство("ПериодичностьОповещенияВДнях") Тогда
	ПериодичностьОповещенияВДнях = ДанныеПараметров.ПериодичностьОповещенияВДнях;
КонецЕсли;

Если ПериодичностьОповещенияВДнях > 0 Тогда

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДатаНачала = НачалоДня(Выборка.ДатаУстановки);
		ТекущийДень = НачалоДня(ТекущаяДата());
		
		РазницаВДнях = (ТекущийДень - ДатаНачала) / 86400;
		
		Если РазницаВДнях > 0 И РазницаВДнях % ПериодичностьОповещенияВДнях = 0 Тогда
		    //Если в день отправки уведомления была ошибка в коде, то сообщение не будет отправлено до следующего периода отправки
			МассивПользователейПолучателей = Новый Массив;
			МассивПользователейПолучателей.Добавить(Сотрудники.ПользовательСотрудника(Выборка.Подготовил));
			МассивПользователейПолучателей.Добавить(Сотрудники.ПользовательСотрудника(Выборка.ОтветственныйЗаКатегорию));
			МассивПользователейПолучателей.Добавить(Сотрудники.ПользовательСотрудника(Выборка.ОсновнойМенеджерКонтрагента));
			
			Тема = "Напоминание по " + Выборка.Документ;
			Тело = СтрШаблон(
					"Уведомляем о том, что с момента оформления документов по %1 прошло %2 дней.
					|
					|Ссылка на документ: %3",
					Выборка.Документ,
					РазницаВДнях,
					РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(Выборка.Документ));
			
			ТекстУведомления = Тема + Символы.ПС + Тело;
			
			Для каждого ПользовательПолучатель Из МассивПользователейПолучателей Цикл
			
				РегистрыСведений.ОчередьУведомлений.ДобавитьУведомление(
					ПользовательПолучатель,
					УМ_ДинамическиеКонстантыПовтИсп.ПолучитьЗначениеДинамическойКонстанты("ВидПроизвольногоСобытия"),
					Перечисления.СпособыУведомления.ПоПочте,
					Выборка.Документ,
					Выборка.Документ,
					ТекстУведомления);
			
			КонецЦикла;
		
		КонецЕсли;
		
	КонецЦикла;	

КонецЕсли;',
  'Комментарий': ''
 },
 'ТЧ': [
  {
   'Переменные': [
    {
     'Имя': 'ПериодичностьОповещенияВДнях',
     'Разделитель': null,
     'ЗначениеСтрока': '',
     'Комментарий': ''
    }
   ]
  }
 ]
}
