{
 'Реквизиты': {
  'Наименование': 'Интеграция с УПП (Возврат оборудования - Статус документа)',
  'ТекстОбработки': 'ДанныеПодключений = Новый Структура;
Для каждого ДанныеПеременной Из Переменные Цикл
	ДанныеПодключений.Вставить(ДанныеПеременной.Имя, ДанныеПеременной.Значение);
КонецЦикла;

ТипСобытия = Справочники.УМ_ТипыСобытий.ПолучитьТипСобытияПоИдентификатору("ИнтеграцияУПП_ВозвратОборудования");
	
Запрос = Новый Запрос;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	DT_ДО_ВнешниеСистемы.Ссылка КАК Ссылка
	|ИЗ
	|	ПланОбмена.DT_ДО_ВнешниеСистемы КАК DT_ДО_ВнешниеСистемы
	|ГДЕ
	|	DT_ДО_ВнешниеСистемы.ВыгружатьВозвратОборудования
	|	И НЕ DT_ДО_ВнешниеСистемы.ПометкаУдаления
	|	И НЕ DT_ДО_ВнешниеСистемы.ЭтотУзел";
РезультатЗапроса = Запрос.Выполнить();
ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	Узел = ВыборкаДетальныеЗаписи.Ссылка;
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(Узел, Узел.НомерОтправленного);
	
	СообщениеСформировано = Ложь;
	
	Пока ВыборкаИзменений.Следующий() Цикл
	
		РегОбъект = ВыборкаИзменений.Получить();

		Если ТипЗнч(РегОбъект) = Тип("РегистрСведенийНаборЗаписей.ТекущиеСостоянияДокументов") Тогда
			
			Документ = РегОбъект.Отбор.Документ.Значение;
			
			НастройкиПоВидуДокумента = DT_ОбщегоНазначениеПовтИсп.ПолучитьНастройкиПоВидуДокумента(Документ.ВидДокумента);
		
			Если НастройкиПоВидуДокумента.ФункционалВозвратаОборудования Тогда
				
				Статус = "Проект";
				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	ДанныеДокументовПредприятия.СостояниеОбработки КАК СостояниеОбработки
					|ИЗ
					|	РегистрСведений.ДанныеДокументовПредприятия КАК ДанныеДокументовПредприятия
					|ГДЕ
					|	ДанныеДокументовПредприятия.Документ = &Документ
					|
					|УПОРЯДОЧИТЬ ПО
					|	ДанныеДокументовПредприятия.ДатаСортировки УБЫВ";
				Запрос.УстановитьПараметр("Документ", Документ);
				РезультатЗапроса = Запрос.Выполнить();
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					Статус = Строка(Выборка.СостояниеОбработки);
				КонецЦикла;
				
				СсылкаНаДопДанные = DT_ОбщегоНазначенияСерверВызовСервера.ПолучитьСсылкуНаДопДанныеДокумента(Документ, "DT_Данные_ВозвратОборудования");
				
				СтруктураДанных = Новый Структура;
				СтруктураДанных.Вставить("UIDMessage"	, Строка(Новый УникальныйИдентификатор));
				СтруктураДанных.Вставить("GUIDDoc"		, XMLСтрока(Документ));
				СтруктураДанных.Вставить("Status"		, Статус);
				СтруктураДанных.Вставить("ID"			, СсылкаНаДопДанные.Идентификатор);
				
				Соединение = Неопределено;
				Если ДанныеПодключений.Свойство("ВозвратОборудования_СтатусДокумента") Тогда
					Соединение = ДанныеПодключений.ВозвратОборудования_СтатусДокумента;
				КонецЕсли;
				Если Соединение <> Неопределено Тогда
					СообщениеСформировано = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если СообщениеСформировано Тогда
			НачатьТранзакцию();
			Попытка
				РегистрыСведений.УМ_ОчередьСообщенийКОтправке.ДобавитьСообщение(
					ТипСобытия,
					Соединение,
					СтруктураДанных,
					СтруктураДанных.UIDMessage);	
				
				//Снимем регистрацию с плана обмена
				ПланыОбмена.УдалитьРегистрациюИзменений(Узел, РегОбъект);
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
			
				ТекстОшибки = ОписаниеОшибки();
			
				ЗаписьЖурналаРегистрации(
					"DT_ИнтеграцияСУПП",
					УровеньЖурналаРегистрации.Ошибка,,,
					"Ошибка при интеграции с УПП(возврат оборудования). Не удалось записать новое сообщение для отправки." + Символы.ПС + ТекстОшибки);
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
		
КонецЦикла;
   
УМ_Интеграции.Алгоритм("ОтправитьСообщениеВУППпоТипуСобытия",
	ТипСобытия,
 	"DT_ИнтеграцияСУПП",
 	"УПП(возврат оборудования)");',
  'Комментарий': ''
 },
 'ТЧ': [
  {
   'Переменные': [
    {
     'Имя': 'ВозвратОборудования_СтатусДокумента',
     'Разделитель': null,
     'ЗначениеСтрока': '',
     'Комментарий': ''
    }
   ]
  }
 ]
}
