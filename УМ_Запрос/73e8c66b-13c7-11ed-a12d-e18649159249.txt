{
 'Реквизиты': {
  'Наименование': 'Загрузка AD DWH',
  'ТекстОбработки': '//-----------------------------------------------------------------
//Параметры выгрузки
//-----------------------------------------------------------------
Отладка = Ложь; 

//-----------------------------------------------------------------  
//Служебные параметры  
//----------------------------------------------------------------- 

ДатаЗагрузки = ТекущаяДата();
СистемаGUID = XMLСтрока(Система);  
//[todo] в текст запроса можно ставить условие по whenChanged и whenCreated для оптимизации
ТекстЗапроса = ПолучитьЗначениеПеременной(Переменные,"ТекстЗапроса");
СоединениеDWH = ПолучитьЗначениеПеременной(Переменные,"СоединениеDWH");
Если СоединениеDWH = неопределено Тогда
	ВызватьИсключение "Не заполнен параметр СоединениеDWH";
КонецЕсли;
СоединениеDWH = ПолучитьСоединениеПоПодключению(СоединениеDWH,СтрокаОшибок);
Если СоединениеDWH = неопределено Тогда
	ВызватьИсключение "Не выполнено соединение к DWH";
КонецЕсли; 

ПорцияВыборки = 1000; //обязательно ограничение

Обработано = 0;
Добавлено = 0;
Обновлено = 0;
Удалено = 0;
	
//-----------------------------------------------------------------  
//Выполнение  
//----------------------------------------------------------------- 

		              
ТЗ = новый ТаблицаЗначений;
ТЗ.Колонки.Добавить("UUID",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(72)));
ТЗ.Колонки.Добавить("Система",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(36)));
ТЗ.Колонки.Добавить("GUID",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(36)));
ТЗ.Колонки.Добавить("ДатаЗагрузки",Новый ОписаниеТипов("Дата"));
  
ТЗ.Колонки.Добавить("ADsPath",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(500)));
ТЗ.Колонки.Добавить("Type",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));                                                                                               
ТЗ.Колонки.Добавить("DisplayName",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("employeeID",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
ТЗ.Колонки.Добавить("company",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("mail",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("telephoneNumber",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("sAMAccountName",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("userPrincipalName",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("streetAddress",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("UserAccountControl",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("sn",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("givenName",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("name",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("distinguishedName",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("whenChanged",Новый ОписаниеТипов("Дата"));
ТЗ.Колонки.Добавить("cn",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("Description",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("department",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("title",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("mobile",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150)));
ТЗ.Колонки.Добавить("isActive",Новый ОписаниеТипов("Булево"));
ТЗ.Колонки.Добавить("extensionAttribute1",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150))); //SiteSD - имя площадки для SD
ТЗ.Колонки.Добавить("extensionAttribute2",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150))); //departmentSD - имя отдела для SD
ТЗ.Колонки.Добавить("extensionAttribute4",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150))); //Группа рассылки "Сотрудник группы компаний"
ТЗ.Колонки.Добавить("extensionAttribute5",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150))); //Группа рассылки "Место работы"

НомерПорции = 0;
ПродолжатьВыборку = истина;
PartValue = ""; //уникальное значение для разделения выборки по частям. Если значение не уникально, то при попадании на разрыв порции будут перевыгружены все
ОшибкаВыборки = Ложь; 
Пока ПродолжатьВыборку Цикл
	НомерПорции = НомерПорции + 1;
    Если НомерПорции > 100 Тогда
		ВызватьИсключение "Превышено максимально количество порций запроса. возможно ошибка в алгоритме"
	КонецЕсли;     
	ТекущийТекстЗапроса = стрзаменить(ТекстЗапроса,"[From]",Соединение.from);
	ТекущийТекстЗапроса = стрзаменить(ТекущийТекстЗапроса,"[TOP]","TOP "+Формат(ПорцияВыборки,"ЧГ=")+" ");
	Если PartValue = "" Тогда
		//первый кусок не ставим условия
		ТекущийТекстЗапроса = стрзаменить(ТекущийТекстЗапроса,"[ANDWHEREPart]","");
	Иначе
		//следующий кусок, ставим условия
		ТекущийТекстЗапроса = стрзаменить(ТекущийТекстЗапроса,"[ANDWHEREPart]"," and objectGUID > "+PartValue+""); 
		
	КонецЕсли; 
	Сообщить(ТекущийТекстЗапроса);
	ЗаписиSQL = Соединение.Connection.Execute(ТекущийТекстЗапроса);		
	Если ЗаписиSQL.State = 1 Тогда 
		Пока ЗаписиSQL.EOF=0 Цикл
			Обработано = Обработано + 1;
			НовСтр = ТЗ.Добавить();
			НовСтр.Система = СистемаGUID;
			
			МассивobjectGUID = ЗаписиSQL.Fields("objectGUID").Value.Выгрузить();
			GUID = "";
			Для каждого стр из МассивobjectGUID Цикл
				GUID = GUID + стр;
			КонецЦикла;
			НовСтр.GUID = GUID;
			НовСтр.UUID = НовСтр.Система+НовСтр.GUID;
			НовСтр.ДатаЗагрузки = ДатаЗагрузки;
			НовСтр.whenChanged = ЗаписиSQL.Fields("whenChanged").Value;
			НовСтр.ADsPath = ЗаписиSQL.Fields("ADsPath").Value;
			
			Type = ""; //вычисляем тип из массива
            МассивObjectClass = ЗаписиSQL.Fields("objectClass").value.Выгрузить();
			Для каждого Эл из МассивObjectClass Цикл
				Если Эл = "user" Тогда
					Type = "user"; Прервать;
				ИначеЕсли Эл = "contact" Тогда
					Type = "contact"; Прервать;
				КонецЕсли;
			КонецЦикла;
			НовСтр.Type = Type;
			
			UserAccountControl = ЗаписиSQL.Fields("UserAccountControl").Value;
			Если UserAccountControl <> NULL Тогда
				НовСтр.isActive = ((UserAccountControl % 4) = 0);
			Иначе
				НовСтр.isActive = Ложь;
			КонецЕсли;
			НовСтр.DisplayName = ЗаписиSQL.Fields("DisplayName").Value;
			НовСтр.employeeID = ЗаписиSQL.Fields("employeeID").Value;
			НовСтр.sAMAccountName = ЗаписиSQL.Fields("sAMAccountName").Value;
			НовСтр.company = ЗаписиSQL.Fields("company").Value;
			НовСтр.mail = ЗаписиSQL.Fields("mail").Value;
			НовСтр.telephoneNumber = ЗаписиSQL.Fields("telephoneNumber").Value;
			НовСтр.userPrincipalName = ЗаписиSQL.Fields("userPrincipalName").Value;
			НовСтр.streetAddress = ЗаписиSQL.Fields("streetAddress").Value;
			НовСтр.sn = ЗаписиSQL.Fields("sn").Value;
			НовСтр.givenName = ЗаписиSQL.Fields("givenName").Value;
			НовСтр.name = ЗаписиSQL.Fields("name").Value;
			НовСтр.distinguishedName = ЗаписиSQL.Fields("distinguishedName").Value;
			НовСтр.whenChanged = ЗаписиSQL.Fields("whenChanged").Value;
				
			НовСтр.cn = ЗаписиSQL.Fields("cn").Value;
			НовСтр.extensionAttribute1 = ЗаписиSQL.Fields("extensionAttribute1").Value;
			НовСтр.extensionAttribute2 = ЗаписиSQL.Fields("extensionAttribute2").Value;
			НовСтр.extensionAttribute4 = ЗаписиSQL.Fields("extensionAttribute4").Value;
			НовСтр.extensionAttribute5 = ЗаписиSQL.Fields("extensionAttribute5").Value;	
				
			DescriptionArray = ЗаписиSQL.Fields("description").Value;
			Если DescriptionArray = NULL Тогда
				НовСтр.Description = "";
			Иначе
				НовСтр.Description = СтрСоединить(DescriptionArray.Выгрузить(),"");	
			КонецЕсли;
			НовСтр.department = ЗаписиSQL.Fields("department").Value;
			НовСтр.title = ЗаписиSQL.Fields("title").Value;
			НовСтр.mobile = ЗаписиSQL.Fields("mobile").Value;

			
			//в тексте запроса не работает TOP, поэтому ограничу его в переборе
			Если Обработано%ПорцияВыборки <> 0 Тогда
				//продолжаем
				ЗаписиSQL.MoveNext(); 
			Иначе 
				//посчитаем партию выборки 
				//тут странный формат для запроса
				PartValue = "\u0027"; //для выборки по кускам
				тОснование = 16;
				Для каждого тЗначение из МассивobjectGUID Цикл
					тРезультат = "";		
					Пока тЗначение > 0 Цикл
						тРезультат = Сред("0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ", тЗначение%тОснование + 1, 1) + тРезультат;
						тЗначение = Цел(тЗначение/тОснование);
					КонецЦикла; 
					
					ДлинаРез = СтрДлина(тРезультат);
					Если ДлинаРез = 0 Тогда
						тРезультат = "00";
					ИначеЕсли ДлинаРез = 1 Тогда
						тРезультат = "0"+тРезультат;
					ИначеЕсли ДлинаРез > 2 Тогда
						ВызватьИсключение "Ошибка преобразования GUID";
					КонецЕсли;
					
					PartValue = PartValue + "\\"+тРезультат;
				КонецЦикла;
				PartValue = PartValue + "\u0027";
				
				Прервать; //выходим, так как уперлись в предел
			КонецЕсли;
				
		КонецЦикла; 
		
		//закончили выборку  
		Если Обработано = 0 Тогда
			ПродолжатьВыборку = Ложь;
			Сообщить("Нет данных в выборке");	
		ИначеЕсли Обработано % ПорцияВыборки <> 0 Тогда
			//не уперлись в предел выборки, значит выбрали все
			ПродолжатьВыборку = Ложь;
			Сообщить("не уперлись в предел выборки, значит выбрали все. колво "+Обработано);
		Иначе
			Сообщить("уперлись в предел выборки, будем делать еще выборку. колво "+Обработано);
		КонецЕсли;
	
	Иначе
		//пустая выборка, возможно ошибка
		ПродолжатьВыборку = Ложь;
		Сообщить("пустаяВыборка");
		ОшибкаВыборки = истина;	
	КонецЕсли;

		
КонецЦикла;	

 
Если не ОшибкаВыборки Тогда	
	ПараметрыЗаписиВSQL = DT_DWH.ПараметрыЗаписиВSQL();
	ПараметрыЗаписиВSQL.ТЗ = ТЗ; 
	ПараметрыЗаписиВSQL.ИмяТаблицы = "AD"; 
	ПараметрыЗаписиВSQL.Ключи.Добавить("UUID");
	ПараметрыЗаписиВSQL.СвойстваДляСравнения.Добавить("whenChanged"); 
	ПараметрыЗаписиВSQL.СвойстваДляСравнения.Добавить("ADsPath"); //при изменении пути, whenChanged не меняется 
    ПараметрыЗаписиВSQL.КлючиОбластиУдаления.Вставить("Система",СистемаGUID); //подчищать удаленные настройки (если сотрудник или его прием удален)
		
	СтруктураОтвета = DT_DWH.ЗаписатьТЗВSQL(СоединениеDWH,ПараметрыЗаписиВSQL);
	Добавлено = СтруктураОтвета.Добавлено;
	Обновлено = СтруктураОтвета.Обновлено;
    Удалено = СтруктураОтвета.Удалено;
    
	Если не Отладка Тогда
		УстановитьДатуПоследнейЗагрузки(Система,ЗапросСсылка,ДатаЗагрузки);
	КонецЕсли; 
КонецЕсли;

СтрокаОшибок = СтрокаОшибок + ?(СтрокаОшибок="","",Символы.ПС)+ Система +  " Обработано - "+Обработано+". Добавлено - "+Добавлено+". Обновлено - "+Обновлено+". Удалено - "+Удалено;',
  'Комментарий': 'выборка порционная, порции по objectGUID'
 },
 'ТЧ': [
  {
   'Переменные': [
    {
     'Имя': 'ТекстЗапроса',
     'Разделитель': null,
     'ЗначениеСтрока': 'SELECT 
    ADsPath,
    DisplayName,
    employeeID,
    company,
    mail,
    telephoneNumber,
    sAMAccountName,
    userPrincipalName,
    streetAddress,
    UserAccountControl,
    objectGUID,
    sn,
    givenName,
    name,
    distinguishedName,
    whenChanged,
    cn,
    Description,
    manager,
    department,
    title,
    mobile, 
    objectClass,
    extensionAttribute1,
    extensionAttribute2,
	extensionAttribute4,
	extensionAttribute5

FROM 
    [From]
    
WHERE
    objectCategory = \u0027Person\u0027 and (objectClass=\u0027user\u0027 or objectClass=\u0027contact\u0027)
    [ANDWHEREPart]
    
ORDER BY objectGUID',
     'Комментарий': ''
    },
    {
     'Имя': 'СоединениеDWH',
     'Разделитель': null,
     'ЗначениеСтрока': '',
     'Комментарий': ''
    }
   ]
  }
 ]
}
